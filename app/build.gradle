/*
 * Copyright (C) 2020 Oleg Ivashchenko <olegivo@gmail.com>
 *
 * This file is part of AFS.
 *
 * AFS is free software: you can redistribute it and/or modify
 * it under the terms of the MIT License.
 *
 * AFS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * AFS.
 */

buildscript {
    repositories {
        google()
    }

    dependencies {
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.0.1'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.2.1'
    }
}

plugins {
    id 'se.patrikerdes.use-latest-versions' version '0.2.14'
    id 'com.github.ben-manes.versions' version '0.30.0'
}
repositories {
    mavenCentral()
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.firebase.appdistribution'

apply plugin: 'com.google.firebase.crashlytics'

apply plugin: "org.jmailen.kotlinter"

apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "ru.olegivo.afs"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 7
        versionName "0.2.5"
        testInstrumentationRunner "ru.olegivo.afs.CustomAndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.incremental"   : "true",
                        "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }

        // used by Room, to test migrations
        sourceSets {
            androidTest.assets.srcDirs +=
                    files("$projectDir/schemas".toString())
        }
    }
    buildTypes {
        all {
            def apiAddress = "https://mobifitness.ru/"
            String debugMode

            switch (name) {
                case "debug":
                    debugMode = 'true'
                    break
                case "release":
                    debugMode = 'false'
                    break
            }

            buildConfigField "String", "API_URL", "\"${apiAddress}\""
            buildConfigField "String", "DB_NAME", "\"afs\""
            buildConfigField "Boolean", "DEBUG_MODE", debugMode

            firebaseAppDistribution {
                serviceCredentialsFile = "${projectDir}/firebase-app-distribution.json"
                def file = new File(serviceCredentialsFile)
                if (!file.exists()) println "firebaseAppDistribution.serviceCredentialsFile not exists: $serviceCredentialsFile"
                groups = "Alpha"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/licenses/ASM'
        exclude("META-INF/*.kotlin_module")
        exclude "**/attach_hotspot_windows.dll"
    }
}

android.sourceSets {
    test {
        java.srcDirs += "$projectDir/src/testShared/java"
    }

    androidTest {
        java.srcDirs += "$projectDir/src/testShared/java"
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'androidx.arch.core'
                && !details.requested.name.contains('core-runtime')) {
            details.useVersion "2.0.1"
        }
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    checkForGradleUpdate = false

    // Example 1: reject all non stable versions
    rejectVersionIf {
        isNonStable(candidate.version)
    }

    // Example 2: disallow release candidates as upgradable versions from stable versions
    rejectVersionIf {
        isNonStable(candidate.version) && !isNonStable(currentVersion)
    }

    // Example 3: using the full syntax
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}

dependencies {
    def assertJVersion = '3.17.2'
    def coroutinesVersion = "1.3.9"
    def daggerVersion = "2.28.3"
    def javaxInjectVersion = '1'
    def jUnitVersion = '4.13'
    def ktorVersion = "1.4.0"
    def mockitoVersion = '3.5.10'
    def moshiVersion = "1.10.0"
    def mockitoKotlinVersion = '2.2.0'
    def recyclerViewVersion = "1.1.0"
    def retrofitVersion = "2.9.0"
    def roomVersion = "2.2.5"
    def workVersion = "2.4.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation "androidx.work:work-runtime:$workVersion"
    implementation "androidx.work:work-runtime-ktx:$workVersion"
    implementation "androidx.work:work-rxjava2:$workVersion"

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    implementation 'com.google.android.material:material:1.2.1'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation platform('com.google.firebase:firebase-bom:25.9.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'

    implementation "com.squareup.moshi:moshi:${moshiVersion}"
    implementation "com.squareup.moshi:moshi-adapters:${moshiVersion}"
    implementation "com.squareup.moshi:moshi-kotlin:${moshiVersion}"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:${moshiVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-moshi:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'

    implementation "javax.inject:javax.inject:${javaxInjectVersion}"

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutinesVersion")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-cbor:1.0.0-RC")

    implementation("io.ktor:ktor-client-core:$ktorVersion")
    implementation("io.ktor:ktor-client-cio:$ktorVersion")
    implementation("io.ktor:ktor-client-okhttp:$ktorVersion")
    implementation("io.ktor:ktor-client-serialization:$ktorVersion")
    implementation("io.ktor:ktor-client-serialization-jvm:$ktorVersion")

    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'


    implementation 'ru.terrakok.cicerone:cicerone:5.1.1'

    testImplementation "junit:junit:${jUnitVersion}"
    testImplementation "org.assertj:assertj-core:${assertJVersion}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${coroutinesVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:${mockitoKotlinVersion}"

    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$daggerVersion"

    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:${mockitoKotlinVersion}"

    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.11.0'

    androidTestImplementation "org.mockito:mockito-android:${mockitoVersion}"

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${coroutinesVersion}"

    androidTestImplementation "org.assertj:assertj-core:${assertJVersion}"
}

kotlinter {
    disabledRules = [
            "import-ordering",
            "comment-spacing",
            "no-wildcard-imports",
            "curly-spacing"
    ]
}

apply from: "jacoco.gradle"

task staticAnalysis(
        dependsOn: [
                'lintKotlin',
                'detekt'
        ]) {
    group = 'verification'
}

task staticAnalysisFull(
        dependsOn: [
                'staticAnalysis',
                'lintDebug'
        ]) {
    group = 'verification'
}

apply plugin: 'com.google.gms.google-services'
